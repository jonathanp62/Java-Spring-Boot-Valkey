/*
 * (#)build.gradle  0.3.0   05/17/2025
 * (#)build.gradle  0.2.0   05/05/2025
 * (#)build.gradle  0.1.0   04/30/2025
 *
 * @author   Jonathan Parker
 * @version  0.3.0
 * @since    0.1.0
 *
 * MIT License
 *
 * Copyright (c) 2025 Jonathan M. Parker
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

/*
 * The following system property must be set in HOME/.gradle/gradle.properties:
 *   systemProp.macosArchitecture=intel -or-
 *   systemProp.macosArchitecture=silicon
 */

plugins {
	id 'java'
	id 'org.springframework.boot'				version '3.5.0'
	id 'io.spring.dependency-management'		version '1.1.7'
	id 'net.jmp.gradle.plugin.projectinfo-info'	version '0.1.0'
}

apply plugin: 'net.jmp.gradle.plugin.projectinfo-info'

ext {
	project.group = 'net.jmp.spring.boot.valkey'
	project.version = '0.3.0'
	project.description = 'to demonstrate Spring Boot with Valkey'
}

java.sourceCompatibility = JavaVersion.VERSION_24

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(24)
	}
}

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	implementation		('org.springframework.boot:spring-boot-starter')

	implementation		(variantOf(libs.io.valkey.valkey.glide) {
		classifier 'osx-aarch_64' }
	)

	implementation		(libs.co.elastic.logging.logback.ecs.encoder)
	implementation		(libs.com.esotericsoftware.kryo.kryo5)
	implementation		(libs.com.google.code.gson.gson)
	implementation		(libs.net.jmp.util.logging.logging.utilities)

	testImplementation	('org.springframework.boot:spring-boot-starter-test')

	testImplementation	(libs.net.javacrumbs.json.unit.json.unit.spring)

	testRuntimeOnly		('org.junit.platform:junit-platform-launcher')
}

tasks.compileJava {
    options.forkOptions.jvmArgs = ["-Xmx8192m", "--enable-native-access=ALL-UNNAMED"]
}

tasks.withType(Javadoc).configureEach {
    CoreJavadocOptions cjo = options as CoreJavadocOptions
    StandardJavadocDocletOptions sjdo = options as StandardJavadocDocletOptions

    cjo.encoding("UTF-8")
    cjo.addStringOption("source", "24")
    cjo.memberLevel = JavadocMemberLevel.PRIVATE

    sjdo.version(true)
}

tasks.named('test') {
	useJUnitPlatform()

	testLogging {
		events "passed", "skipped", "failed"
	}
}

bootRun {
	mainClass = 'net.jmp.spring.boot.valkey.MainApplication'
	jvmArgs = [ '-Xmx1g', '--enable-native-access=ALL-UNNAMED' ]

	environment("GRADLE_VERSION", "${gradle.gradleVersion}")
}

tasks.register("bootRunDev") {
	group = "application"
	description = "Runs the Spring Boot application with the dev profile"

	doFirst {
		tasks.bootRun.configure {
			systemProperty("spring.profiles.active", "dev")
			systemProperty("glide.flushDb", "false")
		}
	}

	finalizedBy("bootRun")
}

tasks.register("bootRunTest") {
	group = "application"
	description = "Runs the Spring Boot application with the test profile"

	// The 'test' profile is reserved for unit testing

	doFirst {
		tasks.bootRun.configure {
			systemProperty("spring.profiles.active", "testing")
			systemProperty("glide.flushDb", "true")
		}
	}

	finalizedBy("bootRun")
}
